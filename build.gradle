buildscript {
	repositories {
		plugins
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "net.ltgt.gradle:gradle-apt-plugin:0.12"
	}
}
plugins {
	id "org.gradle.java.experimental-jigsaw" version "0.1.1"
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'net.ltgt.apt'
	apply plugin: 'org.gradle.java.experimental-jigsaw'

	
	group = 'com.zyxist.example.dagger'
	version = '1.0-SNAPSHOT'
	
	sourceCompatibility = '1.9'
	targetCompatibility = '1.9'
	[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
	
	compileJava {
		options.compilerArgs << "-Xlint:unchecked"
		options.compilerArgs << "--add-modules"
		options.compilerArgs << "java.compiler"
	}
	
	sourceSets {
		main {
			java {
				srcDirs = ['src/main/java', 'build/generated/source/apt/main']
			}
		}
	}
	
	// workaround for Java compiler bug that crashes JVM during the compilation,
	// if the directory with generated sources already exists: 
	task cleanGeneratedSources (type: Delete) {
		delete 'build/generated/source/apt/main'
	}
	compileJava.dependsOn cleanGeneratedSources

	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		compile 'com.google.dagger:dagger:2.11'
		compile 'com.google.guava:guava:23.0'
		apt 'com.google.dagger:dagger-compiler:2.11'
	}
}
